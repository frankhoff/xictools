
[Setup]

AppName=wrspice
AppVerName=wrspice-VERSION
AppVersion=VERSION
AppPublisher=Whiteley Research Inc.
AppPublisherURL=http://wrcad.com
AppMutex=wrspiceMutex

OutputBaseFilename=xictools_wrspice-OSNAME-VERSION-ARCH

Compression=bzip

DefaultDirName=c:\usr\local
DefaultGroupName=XicTools
DirExistsWarning=no

PrivilegesRequired=admin

UninstallFilesDir={app}\xictools\wrspice.current\uninstall
UninstallDisplayIcon={app}\xictools\wrspice.current\bin\wrspice.exe

[Files]

Source: "..\root\usr\local\xictools\wrspice.current\bin\preinstall.bat"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\postinstall.bat"; DestDir: "{app}\xictools\wrspice.current\bin"; BeforeInstall: DoBeforeInstall
Source: "..\root\usr\local\xictools\wrspice.current\bin\wrspice.exe"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\wrspice.dll"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\wrspiced.exe"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\mmjco.exe"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\multidec.exe"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\proc2mod.exe"; DestDir: "{app}\xictools\wrspice.current\bin";
Source: "..\root\usr\local\xictools\wrspice.current\bin\printtoraw.exe"; DestDir: "{app}\xictools\wrspice.current\bin"; AfterInstall: DoAfterInstall

Source: "..\root\usr\local\xictools\wrspice.current\examples\*"; DestDir: "{app}\xictools\wrspice.current\examples"
Source: "..\root\usr\local\xictools\wrspice.current\examples\JJexamples\*"; DestDir: "{app}\xictools\wrspice.current\examples\JJexamples"
Source: "..\root\usr\local\xictools\wrspice.current\examples\JJexamples_old\*"; DestDir: "{app}\xictools\wrspice.current\examples\JJexamples_old"

Source: "..\root\usr\local\xictools\wrspice.current\help\*"; DestDir: "{app}\xictools\wrspice.current\help"
Source: "..\root\usr\local\xictools\wrspice.current\help\screenshots\*"; DestDir: "{app}\xictools\wrspice.current\help\screenshots"

Source: "..\root\usr\local\xictools\wrspice.current\startup\*"; DestDir: "{app}\xictools\wrspice.current\startup"
Source: "..\root\usr\local\xictools\wrspice.current\startup\devices\*"; DestDir: "{app}\xictools\wrspice.current\startup\devices"

Source: "..\root\usr\local\xictools\wrspice.current\scripts\*"; DestDir: "{app}\xictools\wrspice.current\scripts"

Source: "..\root\usr\local\xictools\wrspice.current\docs\wrs*"; DestDir: "{app}\xictools\wrspice.current\docs";
Source: "..\root\usr\local\xictools\wrspice.current\docs\README"; DestDir: "{app}\xictools\wrspice.current\docs"
Source: "..\root\usr\local\xictools\wrspice.current\docs\MSWINFO.TXT"; DestDir: "{app}\xictools\wrspice.current\docs"

[Icons]

Name: "{group}\wrspice"; Filename: "{app}\xictools\wrspice.current\bin\wrspice.bat"; WorkingDir: "c:\"; IconFilename: "{app}\xictools\wrspice.current\bin\wrspice.exe"

[code]

#include "..\..\..\xt_base\packages\files\inno_uninstall"

function InitializeSetup(): Boolean;
begin
  Result := True;
end;

procedure DoBeforeInstall();
var
    resultCode:  Integer;
begin
    Exec(ExpandConstant('{app}\xictools\wrspice.current\bin\preinstall.bat'),
        '', ExpandConstant('{app}\xictools'), SW_HIDE, ewWaitUntilTerminated,
        resultCode);
end;

procedure DoAfterInstall();
var
    resultCode:  Integer;
begin
    Exec(ExpandConstant('{app}\xictools\wrspice.current\bin\postinstall.bat'),
        '', ExpandConstant('{app}\xictools'), SW_HIDE, ewWaitUntilTerminated,
        resultCode);
end;

function InitializeUninstall(): Boolean;
begin
    DoBeforeInstall();
    Result := True;
end;

